plugins {
    id 'com.jaredsburrows.license'
    id 'org.openjfx.javafxplugin' version '0.0.7'
}

javafx {
    version = "12"
    modules = [ 'javafx.controls', 'javafx.graphics', 'javafx.fxml', 'javafx.swing' ]
}

task generateData(type: JavaExec) {
	classpath = sourceSets.main.runtimeClasspath
	main = "de.piegames.blockmap.generate.Generator"
	args = ['generateData']
}

task generateSources(type: JavaExec) {
	description 'Generate all enum values from BlockState.java and the version sources'
	classpath = sourceSets.main.runtimeClasspath
	main = "de.piegames.blockmap.generate.Generator"
	args = ["generateVersion;${project.version}"]
}

task generateScreenshots(type: JavaExec) {
	group 'Generation'
	description 'Automatically render certain files and create screenshots from the GUI to show off on Twitter. Takes a really long time to process because a large demo world is generated first.'
	classpath = sourceSets.main.runtimeClasspath
	main = "de.piegames.blockmap.generate.Generator"
	args = ['generateTestWorld', 'generateScreenshots']
}

task run2(type: JavaExec) {
	group 'Application'
	description 'Task run may fail due to a JavaFX bug, use this as a workaround'
	classpath = sourceSets.main.runtimeClasspath
	main = "de.piegames.blockmap.standalone.CommandLineMain"
}

dependencies {
	testCompile 'junit:junit:4.12'

	implementation project(':BlockMap-core')
	implementation project(':BlockMap-gui')
	implementation project(':BlockMap-standalone')
	implementation 'org.apache.logging.log4j:log4j-core:2.11.2'
	implementation 'commons-io:commons-io:2.6'
	implementation 'com.google.guava:guava:27.1-jre'
	implementation 'info.picocli:picocli:3.7.0'
	implementation 'com.github.Minecraft-Technik-Wiki:MinecraftLandGenerator:3.1.0'
	implementation group: 'com.codepoetics', name: 'protonpack', version: '1.15'

	/* This is not an actual dependency, but some biome color data has been copied from there so it will show up in the credits list */
	// Currently broken because it does not compile on jitpack
	//implementation 'com.github.toolbox4minecraft:amidst:129d3fb9e0'
}

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java']
		}
		resources {
			srcDirs = ['src/main/resources', "./build/generated-resources/${project.name}/generated-resources-main"]
		}
	}
	test {
		java {
			srcDirs = ['src/test/java']
		}
		resources {
			srcDirs = ['src/test/resources', "./build/generated-resources/${project.name}/generated-resources-test"]
		}
	}
}

test {
	exclude 'de/piegames/blockmap/AllTests.class'
}
